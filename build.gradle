plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "org.sonarqube" version "3.0"
    id "net.ossindex.audit" version "0.4.11"
    id 'org.flywaydb.flyway' version '6.5.4'
}

group 'com.roboautomator'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // For Spring Boot Base
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-validation')

    // For Message Queue
    implementation('org.springframework.boot:spring-boot-starter-activemq')

    // For Database
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.postgresql:postgresql')
    implementation('org.flywaydb:flyway-core')

    // For Swagger Implementation
    implementation('io.springfox:springfox-swagger2:2.9.2')
    implementation('io.springfox:springfox-swagger-ui:2.9.2')

    // For Logging Implementation
    implementation('ch.qos.logback:logback-classic:1.2.3')
    implementation('ch.qos.logback.contrib:logback-json-classic:0.1.5')
    implementation('ch.qos.logback.contrib:logback-jackson:0.1.5')
    implementation('org.apache.logging.log4j:log4j-to-slf4j:2.13.3')
    implementation('org.slf4j:jul-to-slf4j:1.7.30')

    // For Health/Readiness Actuators
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // For utils
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

    // For Testing
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
    testImplementation('org.mockito:mockito-core:3.3.3')
    testImplementation('io.rest-assured:rest-assured:4.2.0')
    testImplementation('io.rest-assured:json-path:4.2.0')
    testImplementation('io.rest-assured:xml-path:4.2.0')

    // Annotations
    annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
}

flyway {
    url = System.getenv('DATABASE_URL') ?: 'jdbc:postgresql://doneux-website-db.cfxlcyr0taj5.eu-west-2.rds.amazonaws.com:5432/doneux_website_backend'
    user = System.getenv('DATABASE_USER') ?: 'postgres'
    password = System.getenv('DATABASE_PASSWORD') ?: 'LBHkjE9gLGp8pnPDvM8q'
    locations = ['classpath:db/migration']
}

// we need to build classes before we can migrate
flywayMigrate.dependsOn classes

apply from: 'gradle/test.gradle'
apply from: 'gradle/test-integration.gradle'
apply from: 'gradle/test-functional.gradle'
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/sonar.gradle'

bootJar {
    manifest {
        attributes 'Start-Class': 'com.roboautomator.app.MainApplication'
    }
    mainClassName = 'com.roboautomator.app.MainApplication'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

configurations.all {
    exclude group:'junit', module:'junit'
    exclude group:'org.junit.vintage', module:'junit-vintage-engine'
    exclude group:'org.mockito', module:'mockito-all'
    exclude group:'org.slf4j', module:'slf4j-log4j12'
    exclude group:'org.springframework.boot', module:'spring-boot-starter-logging'
}
